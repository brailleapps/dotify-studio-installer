defaultTasks 'buildInstaller'

def launch4jDir = 'C:\\Program Files (x86)\\Launch4j'
def nsisDir = 'C:\\Program Files (x86)\\NSIS'
def outputDir = 'ant-build/output'
def resourceDir = 'ant-build/resources'
def tempDir = 'ant-build/temp'

configurations {
	nsis
	launch4j
	app
}

repositories {
    mavenCentral()
    //mavenLocal()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
	app ("org.daisy.dotify:dotify-studio:0.1.0-SNAPSHOT@zip") {
		transitive false
	}
	nsis files('ant-build/resources/nsisant-1.2.jar')
	launch4j fileTree(dir: "${launch4jDir}/lib", include: '*.jar')
	launch4j files("${launch4jDir}/launch4j.jar")
}

task clean {
	doLast() {
		delete tempDir
		delete outputDir
	}
}

task unzip(type: Copy) {
    from zipTree(file(configurations.app.iterator().next()))
    into file("${tempDir}")
    eachFile {details ->
		def targetPath = modifyPath(details.path)
		details.path = targetPath
	}
}

def modifyPath(def path) {
	def modified=path.replaceAll('\\Adotify-studio[^/]*(.+)', {"dotify-studio${it[1]}"})
	println modified
	return modified
}

task makeAppExe(dependsOn: 'unzip') {
	doLast() {
		ant.taskdef(name: 'launch4j',
					classname: 'net.sf.launch4j.ant.Launch4jTask',
					classpath: configurations.launch4j.asPath)
		ant.launch4j(configFile: "${resourceDir}/launch4j.xml")
	}
}

task buildInstaller(dependsOn: 'makeAppExe') {
	doLast() {
		new File("${projectDir}/${outputDir}/dist").mkdirs()
		ant.taskdef(name: 'nsis', 
			classname: 'net.sf.nsisant.Task',
			classpath: configurations.nsis.asPath)
		ant.nsis(script: "${resourceDir}/installer.nsi",
				verbosity: 2,
				path: nsisDir)
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}